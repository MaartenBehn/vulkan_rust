#version 450

#extension GL_AMD_gpu_shader_int16: enable
#extension GL_EXT_shader_explicit_arithmetic_types: enable
#extension GL_EXT_shader_explicit_arithmetic_types_int16: enable

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;


// Octtree Start
#define Bit_8_MAX 255
#define Bit_16_MAX 65535


struct Node{
    uint16_t children[8];
    uvec4 data;
};

layout(binding = 0) buffer OcttreeBuffer {
    Node octtree[];
} octtreeBuffer;

layout(binding = 1) uniform OcttreeInfo {
    uvec4 data_0; // 0 is BufferSize, 1 is Buffer Depth, 2 check Index
} octtreeInfo;


uint getChildIndex(Node node, uint childIndex){
    return node.children[childIndex];
}
void setChildIndex(uint index, uint childIndex, uint newIndex){
    octtreeBuffer.octtree[index].children[childIndex] = uint16_t(newIndex);
}

vec3 getNodeColor(Node node){
    uint rg = node.data[0];
    uint ba = node.data[1];

    vec3 color;
    color.r = rg & Bit_16_MAX;
    color.g = rg >> 16;
    color.b = ba & Bit_16_MAX;

    return color / Bit_16_MAX;
}

uint getNodeId(Node node){
    return node.data[2];
}
uint getNodeIdIndex(uint index){
    return octtreeBuffer.octtree[index].data[2];
}

uint getNodeDepth(Node node){
    return node.data[3] & Bit_8_MAX;
}

bool getRenderFlag(Node node){
    return bool(node.data[3] & (1 << 8));
}
void setRenderFlag(uint index){
    octtreeBuffer.octtree[index].data[3] |= (1 << 8);
}
void resetRenderFlag(uint index){
    octtreeBuffer.octtree[index].data[3] &= ~(1 << 8);
}

Node getNode(uint index){
    return octtreeBuffer.octtree[index];
}
void setNode(uint index, Node node){
    octtreeBuffer.octtree[index] = node;
}

uint getTreeSize(){
    return octtreeInfo.data_0[0];
}
uint getBufferSize(){
    return octtreeInfo.data_0[1];
}
uint getOcttreeDepth(){
    return octtreeInfo.data_0[2];
}
// Octtree End


uint getChildId(uint nodeId, uint childNr, uint depth){
    int childSize = (1 - int(pow(8, depth))) / -7;
    return uint(nodeId + childSize * childNr + 1);
}

void main()
{
    if (gl_GlobalInvocationID.xyz != uvec3(0)){
        return;
    }

    uint writeHead = 1;
    uint readHead = 1;
    uint bufferSize = getBufferSize();

    uint counter = 0;
    while (counter < bufferSize && readHead < bufferSize - 1){
        counter++;

        Node writeNode = getNode(writeHead);
        while (getRenderFlag(writeNode) && writeHead < bufferSize - 1){
            writeHead++;
            writeNode = getNode(writeHead);
        }

        readHead = max(writeHead, readHead) + 1;

        Node readNode = getNode(readHead);
        if (getRenderFlag(readNode)){
            setNode(readHead, writeNode);
            setNode(writeHead, readNode);
        }
    }

    uint octtreeDepth = getOcttreeDepth();

    for (uint nodeIndex = 0; nodeIndex < bufferSize; nodeIndex++){
        Node node = getNode(nodeIndex);
        resetRenderFlag(nodeIndex);

        if (getNodeDepth(node) >= octtreeDepth){
            continue;
        }

        for (uint childNr = 0; childNr < 8; childNr++){
            uint childId = getChildId(getNodeId(node), childNr, octtreeDepth - getNodeDepth(node));

            for (uint childIndex = 0; childIndex < bufferSize; childIndex++){
                if (getNodeIdIndex(childIndex) == childId){
                    setChildIndex(nodeIndex, childNr, childIndex);
                    break;
                }
            }
        }
    }
}
#version 450
#extension GL_EXT_debug_printf : enable
#extension GL_ARB_shading_language_include : require

#include "../shader_includes/ray.glsl"
#include "../shader_includes/dda.glsl"
#include "../shader_includes/debug.glsl"
#include "../shader_includes/node.glsl"

#define MAX_RAY_STEPS 50
#define EPSILON 0.0001
#define TO_1D(pos, size) ((pos.z * size * size) + (pos.y * size) + pos.x)

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout(binding = 0, rgba8) uniform writeonly image2D img;

/*
pub struct RenderBuffer {
    pub pos: Vec3,
    pub screen_size_x: f32,
    pub dir: Vec3,
    pub screen_size_y: f32,
}
*/
layout(binding = 1) uniform UniformBufferObject {
    vec4 pos; // w is screen_size_x
    vec4 dir; // w is screen_size_y
    uint num_chunks;
} render_buffer;

#define POS render_buffer.pos.xyz
#define DIR render_buffer.dir.xyz
#define RES_X render_buffer.pos.w
#define RES_Y render_buffer.dir.w
#define RES vec2(RES_X, RES_Y)
#define NUM_CHUNKS render_buffer.num_chunks

struct ChunkData {
    mat4 transform;
    vec4 aabb_min; // w ist chunk size as u32
    vec4 aabb_max;
};

#define CHUNK_TRANSFORM(chunk_data) chunk_data.transform
#define AABB_MIN(chunk_data) chunk_data.aabb_min.xyz
#define AABB_MAX(chunk_data) chunk_data.aabb_max.xyz
#define CHUNK_SIZE(chunk_data) floatBitsToUint(chunk_data.aabb_min.w)

layout(binding = 2) buffer ChunkDatas {
    ChunkData chunks[];
} chunk_datas;

layout(binding = 3) buffer ChunkNodes {
    uint node_ids[];
} chunk_nodes;

#define GET_NODE_ID(pos, chunk_size, offset) chunk_nodes.node_ids[TO_1D(pos, chunk_size) + offset]

// Voxels


layout(binding = 4) buffer Nodes {
    Node nodes[];
} nodes;
#define GET_NODE(index) nodes.nodes[index]

// Materials 
layout(binding = 5) buffer Mats {
    uint mats[];
} mats;

int next_chunk(in Ray ray, out float t) {
    float best_t = 1000000;
    int best_index = -1;
    for (int i = 0; i < NUM_CHUNKS; i++) {

        ChunkData chunk_data = chunk_datas.chunks[i];

        float t_max;
        if (aabb_ray_test(ray, AABB_MIN(chunk_data), AABB_MAX(chunk_data), t, t_max) && t > 0 && t < best_t) {
            best_t = t;
            best_index = i;
        }
    }

    t = best_t;
    return best_index;
}

bool traverse_chunk(in Ray ray, in float t, in uint chunk_index, out uint step_count) {
    ChunkData chunk_data = chunk_datas.chunks[chunk_index];
    uint chunk_size = CHUNK_SIZE(chunk_data);
    uint chunk_node_id_offset = 0;

    vec3 chunk_pos = (chunk_data.transform * vec4(0, 0, 0, 1)).xyz;
    vec3 global_ray_enter = ray.origen + ray.dir * (t + EPSILON);

    /*
    if (gl_GlobalInvocationID.xy == vec2(640, 360)) {
        debugPrintfEXT("Pos: %f %f %f", local_pos);
    }
    */
    vec3 in_chunk_pos = global_ray_enter - chunk_pos;
    DDA chunk_dda = init_DDA(ray, in_chunk_pos, ivec3(chunk_size));
    DDA node_dda;

    step_count = 0;
    while (step_count < MAX_RAY_STEPS) {
        vec3 node_pos = floor(chunk_dda.pos);
        uint node_id = GET_NODE_ID(ivec3(node_pos), chunk_size, chunk_node_id_offset);
        uint node_index = GET_NODE_INDEX_FROM_NODE_ID(node_id);

        if (node_index != 0) {
            Node node = GET_NODE(node_index);
            Rot rot = GET_ROT_FROM_NODE_ID(node_id);

            float t_min;
            float t_max;
            aabb_ray_test(ray, node_pos, node_pos + 1, t_min, t_max);
            vec3 ray_pos = ray.origen + ray.dir * (t_min + EPSILON);

            vec3 in_node_pos = (ray_pos - node_pos) * NODE_SIZE;
            node_dda = init_DDA(ray, in_node_pos, ivec3(NODE_SIZE));

            while (step_count < MAX_RAY_STEPS) {
                ivec3 voxel_pos = ivec3(node_dda.pos);
                //ivec3 rotated_voxel_pos = ROTATE_VOXEL_POS(voxel_pos, rot);
                uint voxel_index = GET_VOXEL_INDEX_FROM_VOXEL_POS(voxel_pos);
                uint voxel = GET_VOXEL(node, voxel_index);

                if (voxel != 0) {
                    return true;
                }

                node_dda = step_DDA(node_dda);
                if (node_dda.out_of_bounds) {
                    break;
                }

                step_count++;
            }
        }

        chunk_dda = step_DDA(chunk_dda);
        if (chunk_dda.out_of_bounds) {
                break;
            }

            step_count++;

    }

    return false;
}

void main () {
    Ray ray = init_ray(POS, DIR, RES);

    float t;
    int next_chunk = next_chunk(ray, t);

    vec3 color = ray.dir;
    if (next_chunk != -1) {
        color *= 0.7;

        uint step_count;
        if (traverse_chunk(ray, t, next_chunk, step_count)) {

        }

        color = step_count_color(step_count, MAX_RAY_STEPS);
    }

    if (gl_GlobalInvocationID.xy == vec2(640, 360)) {
        color = vec3(1, 0, 0);
    }

    imageStore(img, ivec2(gl_GlobalInvocationID.xy), vec4(color, 0));
}

